#!/usr/bin/perl -w                                         # -*- perl -*-
#
# tt2inst  (bin/tt2inst)
#
# This script installs the optional Template Toolkit components from the 
# 'docs', 'examples', 'images' and 'templates' distribution directories 
# into the corresponding installation directories.  The root directory 
# for the installation should be specified as an argument to the 
# script, e.g. 'tt2inst /usr/local/tt2'
#  

use strict;
use Getopt::Std;
use File::Find;
use File::Path;
use File::Copy;
use File::Spec;
use Cwd;

my $PROGRAM  = 'tt2inst';
my @INSTDIRS = qw( docs examples images templates );

my $args = { };
getopts('vh', $args);
usage() if $args->{ h };

my $verbose = $args->{ v };
my $tt2inst = shift || usage();
my $tt2dist = getcwd;

die <<EOF unless -d "$tt2dist/$INSTDIRS[0]";
This script should be run from the Template Toolkit distribution directory.
EOF

#------------------------------------------------------------------------
# install files 
#------------------------------------------------------------------------

print STDERR <<EOF if $verbose;
Installing optional components into $tt2inst
EOF

foreach my $dir (@INSTDIRS) {
    print STDERR "  + $dir\n"
	if $verbose;

    find(\&install_file, $dir);
}

sub install_file {
    my $f = $File::Find::name;
    return if $f =~ /\bCVS\b/ || m[^docs/html/(?!README)];
    if (-d) {
	my $dir  = File::Spec->catfile($tt2inst, $f);
	mkpath($dir) unless -d $dir;
	return;
    }
    my $dest = File::Spec->catfile($tt2inst, $f);
    copy($_, $dest) || die "$dest: $!\n";
}


#------------------------------------------------------------------------
# usage
#------------------------------------------------------------------------

sub usage {
    print STDERR <<EOF;
$PROGRAM: installation script for optional Template Toolkit components.

usage: $PROGRAM [ -v | -h ] /path/to/installation/root

    -v             verbose mode
    -h             this help
EOF
    exit();
}

